// BASE Spawner
class BaseMonsterSpawner : Actor
{
	default
	{
		+NOTIMEFREEZE
	}
	const SpawnerFlags = SXF_NOCHECKPOSITION |
											 SXF_TRANSFERPOINTERS | 
											 SXF_TRANSFERPITCH | 
											 SXF_TRANSFERROLL | 
											 SXF_TRANSFERAMBUSHFLAG | 
											 SXF_TRANSFERSPECIAL;
}

// Doom Monsters
class ZombiemanSpawner : BaseMonsterSpawner replaces Zombieman
{
	//$Sprite "RNMSA0"
	default
	{
		Radius 20;
		Height 56;
		+ISMONSTER
		+RANDOMIZE
	}
	states
	{
			UpgradeClass2aalt:
				TNT1 A 0 A_SpawnItemEx("PlasmaGuy",0,0,0,0,0,0,0,SpawnerFlags,0,tid);
				stop;

			UpgradeClass2a:
				TNT1 A 0 A_JumpIf( ACS_NamedExecuteWithResult("CheckModType") == 1, "UpgradeClass2aalt"); // Prevents Repeater and Karasawa Zombies from spawning in DVI-DS.
				TNT1 A 0 A_Jump(128, 3);
				TNT1 A 0 A_SpawnItemEx("RepeaterZombie",0,0,0,0,0,0,0,SpawnerFlags,0,tid);
				TNT1 A 0 A_Jump(256, 2);
				TNT1 A 0 A_SpawnItemEx("KarasawaGuy",0,0,0,0,0,0,0,SpawnerFlags,0,tid);
				TNT1 A 0;
				stop;
			UpgradeClass2: 
				TNT1 A 0 A_JumpIf( random(1,320) <= ACS_NamedExecuteWithResult("GetEvolveChance") , "UpgradeClass2a");
				TNT1 A 0 A_SpawnItemEx("PlasmaGuy",0,0,0,0,0,0,0,SpawnerFlags,0,tid);
				stop;

			UpgradeClass:
				TNT1 A 0 A_Jump(128, 3);
				TNT1 A 0 A_Jump(256, "UpgradeClass2");
				TNT1 A 0 A_Jump(256, 2);
				TNT1 A 0 A_SpawnItemEx("RocketLauncherGuy",0,0,0,0,0,0,0,SpawnerFlags,0,tid);
				TNT1 A 0;
				stop;

			UpgradeToChaingunner:
				TNT1 A 0 A_SpawnItemEx("ChaingunGuy",0,0,0,0,0,0,0,SpawnerFlags,0,tid);
				TNT1 A 0;
				stop;

			UpgradeToHellCleric:
				TNT1 A 0 A_SpawnItemEx("HellCleric",0,0,0,0,0,0,0,SpawnerFlags,0,tid);
				TNT1 A 0;
				stop;

			UpgradeToSuicideBomber:
				TNT1 A 0 A_SpawnItemEx("SuicideBomber",0,0,0,0,0,0,0,SpawnerFlags,0,tid);
				TNT1 A 0;
				stop;

		Spawn:
			POSS A 0;
			TNT1 A 0 A_JumpIf( random(1,256) <= 8 && ACS_NamedExecuteWithResult("CheckModType") == 0 && ACS_NamedExecuteWithResult("CheckBombersOption") == 1, "UpgradeToSuicideBomber");
			TNT1 A 0 A_JumpIf( random(1,256) <= 4 && ACS_NamedExecuteWithResult("CheckModType") == 0 && ACS_NamedExecuteWithResult("CheckDVIIMonstersOption") == 1, "UpgradeToHellCleric");
			TNT1 A 0 A_JumpIf( random(1,256) <= 8 && ACS_NamedExecuteWithResult("CheckDoomType") == 1, "UpgradeToChaingunner");
			TNT1 A 0 A_JumpIf( random(1,256) <= ACS_NamedExecuteWithResult("GetEvolveChance") , "UpgradeClass");
			TNT1 A 0 A_SpawnItemEx("DSZombieman",0,0,0,0,0,0,0,SpawnerFlags,0,tid);
			stop;
	}
}
class StealthZombiemanSpawner : ZombiemanSpawner replaces StealthZombieman { }